apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'org.greenrobot.greendao'

def ext = rootProject.ext
def debug_mode = rootProject.ext.debug_mode
android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion ext.build_version

    defaultConfig {
        applicationId ext.application_id
        minSdkVersion ext.min_sdk_version
        targetSdkVersion ext.target_sdk_version
        versionCode ext.version_code
        versionName ext.version_name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        buildConfigField("boolean", "API_ENV", "$debug_mode")
        buildConfigField("String", "UMENG_APP_KEY", "$umeng_app_key")
        buildConfigField("String", "WECHAT_APP_ID", "$wechat_app_id")
        buildConfigField("String", "WECHAT_APP_SECRET", "$wechat_app_secret")
        buildConfigField("String", "QQ_APP_ID", "$qq_app_id")
        buildConfigField("String", "QQ_APP_SECRET", "$qq_app_secret")
        buildConfigField("String", "SINA_APP_ID", "$sina_app_id")
        buildConfigField("String", "SINA_APP_SECRET", "$sina_app_secret")
        buildConfigField("String", "SINA_APP_SHARE_URL", "$sina_app_share_url")
    }

    signingConfigs {
        kl5 {
            storeFile file('../kl5.jks')
            storePassword "123456"
            keyAlias "kl5"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            java.srcDir 'src/common/java'
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    flavorDimensions "id", "channel"
    //维度
    productFlavors {
        kl5 {
            dimension "id"
            applicationId ext.application_id
            if (debug_mode) {//测试环境 + 测试魔蝎appkey
                buildConfigField("String", "DOMAIN", "$debug_domain")
                //buildConfigField("String", "DOMAIN", "$debug_native")
                buildConfigField("String", "H5_DOMAIN", "$debug_h5_domain")
                buildConfigField("String", "H5_DOMAIN_NEW", "$debug_h5_domain")
                buildConfigField("String", "MOXIE_APP_KEY", "$debug_moxie_app_key")
            } else {//生产环境 + 正式环境魔蝎appkey
                buildConfigField("String", "DOMAIN", "$release_domain")
                buildConfigField("String", "H5_DOMAIN", "$release_h5_domain")
                buildConfigField("String", "H5_DOMAIN_NEW", "$release_h5_new_domain")
                buildConfigField("String", "MOXIE_APP_KEY", "$release_moxie_app_key")
            }
            manifestPlaceholders = [
                    GETUI_APP_ID    : debug_mode ? "$debug_getui_app_id" : "$release_getui_app_id",
                    GETUI_APP_SECRET: debug_mode ? "$debug_getui_app_secret" : "$release_getui_app_secret",
                    GETUI_APP_KEY   : debug_mode ? "$debug_getui_app_key" : "$release_getui_app_key",
                    UMENG_APP_KEY   : "$umeng_app_key",
                    qqappid         : "$qq_app_id",
                    PACKAGE_NAME    : applicationId
            ]
            buildConfigField("String", "PATH_USER_AGREEMENT", "$path_user_agreement")
            buildConfigField("String", "PATH_ABOUT_US", "$path_about_us")
            buildConfigField("String", "PATH_HELPER_CENTER", "$path_helper_center")
            signingConfig signingConfigs.kl5
        }

        klyjaz_baidu { dimension "channel" }
        klyjaz_360 { dimension "channel" }
        klyjaz_yingyongbao { dimension "channel" }
        klyjaz_xiaomi { dimension "channel" }
        klyjaz_huawei { dimension "channel" }
        klyjaz_vivo { dimension "channel" }
        klyjaz_oppo { dimension "channel" }
        klyjaz_meizu { dimension "channel" }
        klyjaz_chuizi { dimension "channel" }
        klyjaz_sanxing { dimension "channel" }
        klyjaz_jifeng { dimension "channel" }
        klyjaz_mumayi { dimension "channel" }
        klyjaz_guanfang { dimension "channel" }

        productFlavors.all { flavor ->
            manifestPlaceholders.putAll([
                    UMENG_CHANNEL: name,
                    CHANNEL_ID   : name,
            ])
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //这里修改apk文件名
                def fileName = variant.getBaseName() + ".apk"
                fileName = fileName.substring(fileName.indexOf("-") + 1, fileName.length())
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    lintOptions {
        disable "MissingTranslation"
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':xiaonengchat')
    //魔蝎信用
    compile(name: 'moxie-client-2.2.1', ext: 'aar')
    //坚果信用
    compile(name: 'newnutsdk', ext: 'aar')
    //noinspection GradleCompatible
    compile "com.android.support:cardview-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"
    compile 'com.android.support.constraint:constraint-layout:1.1.2'
    compile 'com.android.support:multidex:1.0.1'
    //Retrofit
    compile('com.squareup.retrofit2:retrofit:2.3.0', {
        exclude module: 'okhttp'
    })
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.3.0'
    //OkHttp
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    //添加拦截日志
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    //RxJava
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //Dagger2
    compile 'com.google.dagger:dagger:2.9'
    apt 'com.google.dagger:dagger-compiler:2.9'
    compile 'org.glassfish:javax.annotation:10.0-b28'
    //ButterKnife
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    //EventBus
    compile 'org.greenrobot:eventbus:3.0.0'
    //Glide Image Loader
    compile 'com.github.bumptech.glide:glide:3.8.0'
    //个推
    compile 'com.getui:sdk:2.10.2.0'
    //umeng统计
    compile 'com.umeng.sdk:common:1.5.0'
    compile 'com.umeng.sdk:analytics:7.5.0'
    //umeng分享，微信，QQ，微博
    compile 'com.umeng.sdk:share-core:6.9.0'
    compile 'com.umeng.sdk:share-qq:6.9.0'
    compile 'com.umeng.sdk:share-wechat:6.9.0'
    compile 'com.umeng.sdk:share-sina:6.9.0'
    //database-greeodao
    compile 'org.greenrobot:greendao:3.2.2'
    //MarqueeView
    compile 'com.sunfusheng:marqueeview:1.3.2'
    //StatusBar
    compile 'com.gyf.barlibrary:barlibrary:2.3.0'
    //RecyclerView Adapter Helper
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.34'
    //round image view
    compile 'com.makeramen:roundedimageview:2.3.0'
    //shaped image view
    compile 'cn.gavinliu.android.lib:ShapedImageView:0.8.4'
    //swipe menu
    compile 'com.github.mcxtzhang:SwipeDelMenuLayout:V1.3.0'
    //rxbinding
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    //hightlight
    compile 'com.isanwenyu.highlight:highlight:1.8.0'
    //chart
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    //Permission
    compile 'com.github.hotchemi:permissionsdispatcher:2.4.0'
    apt "com.github.hotchemi:permissionsdispatcher-processor:2.4.0"
    //webview
    compile 'com.just.agentweb:agentweb:4.0.2'
    //refresh
    compile 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-7'
    compile 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-7'
    //magic indicator
    compile 'com.github.hackware1993:MagicIndicator:1.5.0'
    /*banner*/
    compile 'cn.bingoogolapple:bga-banner:2.2.4@aar'
    compile project(':matisse')
}
